defmodule ErlmasteryWeb.AuthController do
  use ErlmasteryWeb, :controller
  plug Ueberauth

  alias Ueberauth.Strategy.Helpers
  alias Erlmastery.Accounts
  alias ErlmasteryWeb.Authentication

  def request(conn, _params) do
    render(conn, "request.html", callback_url: Helpers.callback_url(conn))
  end

  def delete(conn, _params) do
    conn
    |> Authentication.Plug.sign_out()
    |> put_flash(:info, "You have been logged out!")
    |> clear_session()
    |> redirect(to: "/")
  end

  def callback(%{assigns: %{ueberauth_failure: _fails}} = conn, _params) do
    conn
    |> put_flash(:error, "Failed to authenticate.")
    |> redirect(to: "/")
  end

  def callback(%{assigns: %{ueberauth_auth: auth}} = conn, _params) do
    case Accounts.find(auth) do
      {:ok, user} ->
        conn
        |> Authentication.Plug.sign_in(user)
        |> put_flash(:info, "Successfully authenticated.")
        |> put_session(:current_user, user)
        |> configure_session(renew: true)
        |> redirect(to: "/")

      {:error, reason} ->
        conn
        |> put_flash(:error, reason)
        |> redirect(to: "/")
    end
  end
end
